// Adopted from https://github.com/ratpack/ratpack/blob/master/gradle/dependencies.gradle

ext {
    scalaVersion = '2.11.7'
    scalaInterfaceVersion = '2.11'

    commonVersions = [
            slf4j: '1.7.12',
            log4j: '1.2.17',
            spark: '1.6.2',
            zipkin: '1.4.1-SNAPSHOT',
            cassandra: '2.2.7', // to ensure tests match travis
            akka: '2.4.0'
    ]
}

subprojects { subproject ->
    configurations.all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                if (details.requested.group == 'org.slf4j') {
                  details.useVersion commonVersions.slf4j
                }
                if (details.requested.name == 'scala-library') {
                    details.useVersion scalaVersion
                }
                if (details.requested.group == 'org.apache.cassandra') {
                    details.useVersion commonVersions.cassandra
                }
                if (details.requested.group == 'org.apache.spark') {
                    details.useVersion commonVersions.spark
                }
            }
        }

        // Log4j is bound by Spark
        exclude group: "ch.qos.logback"
        // don't loop slf4j-log4j-slf4j
        exclude module: "log4j-over-slf4j"
     }
}
