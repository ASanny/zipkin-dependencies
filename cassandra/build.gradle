apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
mainClassName = 'io.zipkin.dependencies.spark.cassandra.ZipkinDependenciesJob'

test {
    maxParallelForks 1
    systemProperty 'log4j.configuration', 'org/apache/spark/log4j-defaults.properties'
}

ext {
    versions = [
            cassandraConnector: '1.6.0' // zipkin uses datastax java driver 3.0
    ]
}

dependencies {
    compile "org.apache.spark:spark-core_${scalaInterfaceVersion}:${commonVersions.spark}"
    // avoids compile error: Could not access type DataFrame in package org.apache.spark.sql
    compile "org.apache.spark:spark-sql_${scalaInterfaceVersion}:${commonVersions.spark}"
    compile "com.datastax.spark:spark-cassandra-connector_${scalaInterfaceVersion}:${versions.cassandraConnector}"
    compile "io.zipkin.java:zipkin-storage-cassandra:${commonVersions.zipkin}"
    testCompile "io.zipkin.java:zipkin:${commonVersions.zipkin}:tests"
}

shadowJar {
    zip64 true // spark + zipkin-finagle + scala = lots and lots of classes!
    append('reference.conf') // merge akka resources
}
tasks.build.dependsOn(shadowJar)
artifacts.zipkinUpload shadowJar

configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'com.typesafe.akka' && details.requested.name == "akka-slf4j_${scalaInterfaceVersion}") {
                details.useVersion commonVersions.akka
            }
            if (details.requested.group == 'com.typesafe.akka' && details.requested.name == "akka-remote_${scalaInterfaceVersion}") {
                details.useVersion commonVersions.akka
            }
        }
    }
}
